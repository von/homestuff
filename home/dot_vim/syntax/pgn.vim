" Vim syntax file
" Language: Portable Game Notation (*.pgn)
" Maintainer: Ryan Sarson <rsarson@gmail.com>
" Last Change: 2018-03-27
" Original Author: Charles Ford <ford.charles@gmail.com> 
"                  http://www.vim.org/scripts/script.php?script_id=677
" References: PGN-SPEC
"             https://www.chessclub.com/help/PGN-spec
"             Portable Game Notation
"             https://en.wikipedia.org/wiki/Portable_Game_Notation
"             FIDE Laws of Chess Appendix C. Algebraic notation
"             https://www.fide.com/component/handbook
"             Algebraic Notation
"             https://en.wikipedia.org/wiki/Algebraic_notation_(chess)
" Note: This script interprets PGN files which use standard algebraic
"       notation as required by FIDE (Laws of Chess, Appendix C).
"       Additionally, it recognizes some of the more commonly used
"       notations, as mentioned in the Wikipedia article on algebraic
"       notation.  This allows for syntax highlighting of PGN import format
"       files (see PGN-SPEC).  Examples of these included those generated by
"       Scid (Shane Hudson) and Chessmater (Ubisoft), as well as the many
"       downloadable chess databases available on the Internet.

" for testing
syntax match pgnError /\S/
highlight link pgnError ErrorMsg

" *****************
" *** Tag pairs ***
" *****************
syntax region pgnTagName start=/\[/ end=/\]/ contains=pgnTagValue 
syntax region pgnTagValue start=/"/ skip=/\\"/ end=/"/ contained

" ****************
" *** Movetext ***
" ****************
" move number
" - pgn requires 1.e4 or 1...e5
" - pattern: whitespace number whitespace? period? whitespace? ellipsis?
syntax match pgnMoveNumber /\%(^\|\s\)\zs\d\+\%(\_s*\.\{3,4}\|\%(\%(\_s*\.\)\?\%(\_s*\.\{3}\)\?\)\)\ze\%([^-/]\|\_s\)/
 
" piece
" - fide and pgn do not identify pawn by a letter
syntax match pgnPiece /[KQRBNP♔♕♖♗♘♙♚♛♜♝♞♟]/

" file and/or rank of departure
" - used for disambiguation or in long algebraic notation
syntax match pgnDeparture /\%(ch\)\@!\%(\%([1-8]\)\@1<![a-h][1-8]\?\|[QRBN]\@1<=[1-8]\)\%([-x:]\?[a-h][1-8]\?\%([QRBN=(\/X×+#!?\$]\+\|[:x]\%([^a-h]\|\_s\)\|:\?\%(e\.\?p\.\?\|\%(\%(dbl\|dis\)\.\?\_s*\)\?ch\.\?\|mate\)\|\_s\)\)\@=/

" file or square of arrival
" - the \? after [1-8] allows for unambiguous pawn captures seen in
"   abbreviated algebraic notation, eg, ed for exd6
syntax match pgnArrival /[a-h][1-8]\?\%([QRBN=(\/X×+#!?\$]\+\|[:x]\%([^a-h]\|\_s\)\|:\?\%(e\.\?p\.\?\|\%(\%(dbl\|dis\)\.\?\_s*\)\?ch\.\?\|mate\)\|\_s\)\@=\%(ch\|te\)\@2<!/

" abbreviation
" - fide, pgn, and other commonly used
" - overridden by matches below, eg, promotion, castling
syntax match pgnAbbreviation /[-=()\/:xX×+#]\|\%(\%(0-0\%(-0\)\?\)\|\%(O-O\%(-O\)\?\)\)\|e\.\?p\.\?\|\%(\%(dbl\|dis\)\.\?\_s*\)\?ch\.\?\|mate/

" promotion
" - fide requires new piece to immediately follow arrival square;
"   pgn requires an '=' between arrival square and new piece
syntax match pgnPromotion /\%([a-h][1-8]\?:\?\)\@3<=\%([=/]\?[QRBN]\|([QRBN])\)/

" draw offer
" - required by fide but not part of standard algebraic notation or pgn
syntax match pgnDrawOffer /(=)/

" castling
" - fide requires 0 (zero); pgn requires O (uppercase letter)
syntax match pgnCastling /\%(0-0\%(-0\)\?\)\|\%(O-O\%(-O\)\?\)/

" annotations
" - as used by scid and others
syntax match pgnMoveEvaluation /[!?]\+/

syntax match pgnNovelty /\%(^\|\s\)\zsN\ze\_s/

syntax match pgnPositionEvaluation /\~\|(\@1<!=\%(\%([QRBN]\)\@!\%(\/\?[+∞]\)\?\)\|-\%(-\|\/\)\?+\|+\%(=\|--\?\|\/[=-]\)\|∞/

syntax match pgnDiagram /\%(^\|\s\)\zsD\ze\_s/

syntax match pgnNumericAnnotationGlyph /\$\<\%([0-9]\|[1-9][0-9]\|1[0-9][0-9]\|2[0-4][0-9]\|25[0-5]\)\>/

" variations
" - can be nested; excludes parentheses promotion, draw offer
syntax region pgnVariation start=/\%(([=QRBN])\)\@!(/ end=/)\%(([=QRBN])\)\@3<!/ contains=pgnVariation

" result
" - required by pgn
syntax match pgnResult /1-0\|0-1\|1\/2-1\/2\|½-½\|\*/

" ****************
" *** Comments ***
" ****************
" - cannot be nested
syntax region pgnCommentBlock start=/{/ end=/}/
syntax region pgnCommentSingleLine start=/;/ end=/$/


" "Wow! Look at all the colours, man!"
highlight pgnTagName                 cterm=none       ctermfg=darkmagenta
highlight pgnTagValue                cterm=none       ctermfg=darkblue
highlight pgnMoveNumber              cterm=none       ctermfg=brown
highlight pgnPiece                   cterm=underline  ctermfg=none
highlight pgnDeparture               cterm=none       ctermfg=darkblue
highlight pgnArrival                 cterm=none       ctermfg=none
highlight pgnAbbreviation            cterm=none       ctermfg=darkcyan
highlight pgnPromotion               cterm=italic     ctermfg=none
highlight pgnDrawOffer               cterm=none       ctermfg=darkmagenta
highlight pgnCastling                cterm=none       ctermfg=darkblue
highlight pgnMoveEvaluation          cterm=none       ctermfg=darkred
highlight pgnNovelty                 cterm=none       ctermfg=darkred
highlight pgnPositionEvaluation      cterm=none       ctermfg=darkmagenta
highlight pgnDiagram                 cterm=reverse    ctermfg=darkred
highlight pgnNumericAnnotationGlyph  cterm=none       ctermfg=darkred
highlight pgnVariation               cterm=italic     ctermfg=darkgrey
highlight pgnResult                  cterm=none       ctermfg=darkmagenta
highlight pgnCommentBlock            cterm=none       ctermfg=darkgreen
highlight pgnCommentSingleLine       cterm=none       ctermfg=darkgreen

highlight pgnTagName                 gui=none       guifg=darkmagenta
highlight pgnTagValue                gui=none       guifg=darkblue
highlight pgnMoveNumber              gui=none       guifg=brown
highlight pgnPiece                   gui=underline  guifg=foreground
highlight pgnDeparture               gui=none       guifg=darkblue
highlight pgnArrival                 gui=none       guifg=foreground
highlight pgnAbbreviation            gui=none       guifg=darkcyan
highlight pgnPromotion               gui=italic     guifg=foreground
highlight pgnDrawOffer               gui=none       guifg=darkmagenta
highlight pgnCastling                gui=none       guifg=darkblue
highlight pgnMoveEvaluation          gui=none       guifg=darkred
highlight pgnNovelty                 gui=none       guifg=darkred
highlight pgnPositionEvaluation      gui=none       guifg=darkmagenta
highlight pgnDiagram                 gui=reverse    guifg=darkred
highlight pgnNumericAnnotationGlyph  gui=none       guifg=darkred
highlight pgnVariation               gui=italic     guifg=darkgrey
highlight pgnResult                  gui=none       guifg=darkmagenta
highlight pgnCommentBlock            gui=none       guifg=darkgreen
highlight pgnCommentSingleLine       gui=none       guifg=darkgreen
